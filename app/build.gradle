apply plugin: 'com.android.application'


def getProperty(String propertyFile, String propertyName) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(propertyFile));
    return String.format("\"%s\"", properties.getProperty(propertyName));
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "edu.cnm.deepdive.imgurbrowser2"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL",
                getProperty("$projectDir/../../../services/imgur2.properties", "base_url")
        buildConfigField "String", "CLIENT_ID",
                getProperty("$projectDir/../../../services/imgur2.properties", "client_id")

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
    dataBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Basic support dependencies
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // LiveData & ViewModel dependencies
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Material Design component dependencies
    implementation 'com.google.android.material:material:1.2.1'

    // ReactiveX dependencies
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // recyclerview dependencies
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    // For control over item selection of both touch and mouse driven selection
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-rc03"

    // Picasso - image loading
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Gson (JSON parser) dependency
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit (HTTP client) dependencies
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    // Retrofit (REST client) with ReactiveX & Gson integration
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Fragment navigation dependencies
    implementation 'androidx.navigation:navigation-fragment:2.3.2'
    implementation 'androidx.navigation:navigation-ui:2.3.2'

}
